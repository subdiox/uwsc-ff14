// FFを最前面に表示
FF_ID = GETID("FINAL FANTASY XIV","FFXIVGAME")
WIDTH = 2560
HEIGHT = 1440
ACW(FF_ID,0,0,WIDTH,HEIGHT,0)

WHILE TRUE
  KBD(VK_L,CLICK,1000)

  PRINT "採集箇所の探索を開始します"
  WHILE TRUE
    KBD(VK_1,CLICK,0)
    COLOR = PEEKCOLOR(WIDTH*1280/2560,HEIGHT*158/1440,COL_RGB)
    IF COLOR = 16777215 THEN // 採集場所
      KBD(VK_W,DOWN,0)
      SLEEP(4)
      KBD(VK_W,UP,0)
      BREAK
    ELSEIF COLOR = 16775344 OR COLOR = 15335394 THEN // 敵・NPC
      KBD(VK_SHIFT,DOWN,1000)
      KBD(VK_RETURN,CLICK,1000)
      KBD(VK_SHIFT,UP,1000)
      KBD(VK_A,DOWN,500)
      KBD(VK_A,UP,500)
    ENDIF
    KBD(VK_A,DOWN,200)
    KBD(VK_A,UP,200)
  WEND

  PRINT "採集場所に到着しました"
  KBD(VK_4,CLICK,1000)
  KBD(VK_RETURN,CLICK,1000)
  SLEEP(0.5)
  COLOR = PEEKCOLOR(WIDTH*74/2560,HEIGHT*688/1440,COL_RGB)
  IF COLOR = 16695096 THEN // 簡易採集オン
    PRINT "採集開始"
  ELSEIF COLOR = 5393994 THEN // 簡易採集オフ
    PRINT "簡易採集をオンにします"
    BTN(LEFT,CLICK,74,688,1000)
    PRINT "採集開始"
  ELSE // 選択失敗
    PRINT "採集を開始できません．対象の選択を再試行します"
    KBD(VK_SHIFT,DOWN,1000)
    KBD(VK_RETURN,CLICK,1000)
    KBD(VK_SHIFT,UP,1000)
    CONTINUE
  ENDIF
  KBD(VK_2,CLICK,3000)
  SLEEP(0.5)
  KBD(VK_3,CLICK,3000)
  SLEEP(0.5)
  GATHER(6,WIDTH,HEIGHT)
  SLEEP(10)
  PRINT "採集完了"
  KBD(VK_A,DOWN,500)
  KBD(VK_A,UP,500)

WEND 

// INDEX = 1, 2, ..., 8
FUNCTION GET_Y(INDEX)
  RESULT = INDEX * 65 + 101
FEND

PROCEDURE GATHER(INDEX,WIDTH,HEIGHT)
  BTN(LEFT,CLICK,WIDTH*535/2560,HEIGHT*GET_Y(INDEX)/1440,1000)
FEND